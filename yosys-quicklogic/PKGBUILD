# Maintainer:  Kate Temkin
# Contributor: Stefan Biereigel <$(base64 --decode <<<'c3RlZmFuQGJpZXJlaWdlbC5kZQo=')>
# Contributor: xiretza <xiretza+aur@gmail.com>
# Contributor: Patrick Lloyd <$(base64 --decode <<<'cGF0cmlja0BsbG95ZC5zaAo=')>
# Contributor: Sebastian BÃ¸e <$(base64 --decode <<<'c2ViYXN0aWFuYm9vZUBnbWFpbC5jb20K')>
# Contributor: Darren Wu <$(base64 --decode <<<'ZGFycmVuMTk5NzA4MTBAZ21haWwuY29tCg==')>

pkgname=yosys-quicklogic
pkgrel=1
pkgver=0.6.0
pkgdesc='A framework for RTL synthesis'
arch=('x86_64' 'i686')
url='http://www.clifford.at/yosys/'
license=('custom:ISC')
depends=(tcl libffi python boost-libs)
optdepends=('graphviz: Schematics display support'
            'xdot: Design netlist display support')
makedepends=(git boost)
checkdepends=(iverilog clang)
provides=(yosys)
conflicts=(yosys)
source=("git+https://github.com/QuickLogic-Corp/yosys.git#tag=yosys-${pkgver}"
        "git+https://github.com/berkeley-abc/abc#commit=ed90ce2")
sha512sums=('SKIP'
            'SKIP')

prepare() {
    cp -r abc "${srcdir}/yosys/abc"
}

build() {
    cd "${srcdir}/yosys"

    make config-gcc
    echo "ENABLE_LIBYOSYS=1" >> Makefile.conf
    echo "ENABLE_PYOSYS=1" >> Makefile.conf
    echo "ABCREV=default" >> Makefile.conf

    make PREFIX="/usr"
}

check() {
    cd "${srcdir}/yosys"

    make test
}

package() {
    cd "${srcdir}/yosys"

    # disable stripping in the makefile - duplicated effort and makes debug packages impossible
    make STRIP=':' PREFIX="/usr" PYTHON_PREFIX="${pkgdir}/usr" DESTDIR="${pkgdir}" install

    install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
